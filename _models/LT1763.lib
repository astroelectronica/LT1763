* ----------------------------
*  terminal definitions 
* ----------------------------
*& VIN	 	8
*& VOUT	1
*& ADJUST 	2
*& VGND	3
*& BYPASS	4
*& SHDN   	5
* ------------------------------
*
.SUBCKT LT1763  1  2  3  4  5  8
*
*********************
* power section
*********************
*
* pass transistor
QPWR 51 54 52 Q1
RBASE 54  84  1.72E4
*
* input dependent current limit
VSENS  8 52 DC 0
RSENS  68  0  1.0E3
FSENS 0 68 VSENS  1.0E-3
FCL1 3 60  VSENS  0.096
RCL1 60 3 10
DCL1 60 61 DX
VCL1 61 62 DC 0
ECL1 62 3  8 3  0.03
FCL2  90 3 VCL1 1.0
*
* temperature dependent dropout voltage
RDROP 51 55  RVD  0.12 
*
* quiescent current 
GQC  8  3  80  4  2.03E-5
RQC 1 3  2.4E5
*
* load dependent ground pin current, linear
* A - B - (A*C)
GGPC1 8 3  POLY(3) 68  0  42 0  46  3   0  2.21E-2  -1.52E-7   0  0  0  -8.2E-4    
GGPC2  3  45 POLY(1) 8  2  0.0E-3  1.0E-3
RGPC1 45 3 1.0E3
DGPC1 45 46 DX
RGPC2 46 3 1.0E5
*
* reverse input  protection
ERVP1  40 0 8 3 1.0
DRVP1 40 41 DX
RRVP1 41 42 1.0E3
DRVP2 42 0 DX
RRVP2 42 0 1.0E4
*
* reverse input current
DRVP3 43 8 DX
RRVP3 43 3 1.9E5
*
* reverse output protection
SROP1  55  1   1  8  SROP1
*
********************
* error amplifier
********************
*
* adjust input with +/-7V clamp
RADJ  2  80  10
DAP1 80 89 DAC
DAP2 0  89 DAC
RAPB 80 3 REA 4.436E7
*
GEA1 3 81  80  90  1.78
REA1 81 3 1.0E4
CP1 81  82 1.0E-3
RP1  82 3  110
*
CFEED 84  88 8.5E-9
RFEED 88 90  2.75E3
*
GEA2  3  84  81  3  1.0E-4
REA2  84  3  1.0E4
EEAP  85 3  41 0 1.0
DEAP 84 85 DX
DEAN 86 84 DX
VEAN 86 3  DC 1.4
*
**************************** 
* voltage reference
****************************
*
* temperature dependent 1.22V reference
IREF1 3 90 DC 1.22E-3
RREF1 90 3  RVR 1.0E3 
FLOAD 90 3  VSENS  3.95E-6
*
**************************** 
* noise voltage 
****************************
IR1 0 601 DC 1
IR2 0 602 DC 1
DR1 601 0 DRI
DR2 602 0 DRI
GR1 4  3  601 602  0.13
RR1  4  3  5.9E4
DR3 4 3 DX
DR4 3 4 DX
GR2 90 3  4 3  1.8E-3
*
***************************
* shutdown section
***************************
*
* shdn bias current
GSB 3 70 POLY(2) 42 0 5 3 0 0 0 0 1.33E-3
RSB 70 3 1.0E3
DSB 3 70 DAC 
GSHDN 5 3 70 3 1.48E-7
*
* schmitt trigger, trip points 0.65V / 0.8V
GSD1 3 71 72  5 1.0E-1
RSD1 71 3 1.0E4
CSD1 71 3 2.2E-9
DSDP2 71 73 DTT
VSDP2 73 3 DC 1.8
DSDN2 74 71 DTT
VSDN2 74 3  DC -1.8
GTV1 3 72 POLY(1)  71  3  7.24E-4   2.98E-5
RTV1 72 3 1.0E3
SSHDN  90  3   71  3  SHDN
*
***********************
* models used
***********************
.MODEL DX D IS=8.2592e-016  RS=0 XTI=1 CJO=0 IBV=1.0E-10 TT=0
.MODEL DIC D IS=8.0000E-013 BV=5.0 RS=0 XTI=0
.MODEL DAC D IS=8.0000E-013 BV=6.3 RS=0 XTI=0
.MODEL DTT D IS=8.0000E-016  RS=0 XTI=1 IBV=1.0E-10 TT=8.3E-6
.MODEL DRI D (IS=8.2592e-016 KF=3.2074e-019 RS=0 XTI=1 CJO=0 IBV=1.0E-10 TT=0)
*
.MODEL RVD RES (TC1=6.1E-3)
.MODEL RVR RES (TC1=1.5E-5)
.MODEL REA RES (TC1=0.5E-3)
*
.MODEL SROP1  VSWITCH (RON=0.001  ROFF=1.0E8  VON=0.0  VOFF=0.1)
.MODEL SHDN  VSWITCH (RON=0.01  ROFF=1.0E8  VON=01.0   VOFF=-1.0)
*
.MODEL Q1 PNP IS=1.0E-015  BF=14950 XTI=0 NF=1.0
************************
*
.ENDS LT1763
*
***
*

